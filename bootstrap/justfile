precommit: 
    just format 
    just lint --fix
    just test

format:
    @echo "Formatting ..."
    ruff format
    npx prettier -w *.md ../*.md

lint *args:
    @echo "Linting ..."
    pyright
    ruff check {{args}}
    @echo "Checking for test exclusions in TEST.md ..."
    @grep '!only' TEST.md && exit 1 || true
    @grep '!skip' TEST.md && exit 1 || true

test: test-pytest test-book-md test-md

test-pytest *args:
    # Exclude `test_md.py` because we run it in `test-md`.
    pytest . {{args}} --ignore=test_md.py

[positional-arguments]
test-md *args:
    @cd .. && python3 -m bootstrap.test_md bootstrap/TEST.md -- "$@"

[positional-arguments]
test-book-md *args:
    @cd .. && python3 -m bootstrap.test_md ./book.md -- "$@"

